::    send simple msgs between ships  ::  short description
::
::::
  ::
/?    310                             ::  require hoon version 310 or below
/-    message                         ::  import message structure
::
::
::::  sivtyv-barnel                   ::  name(s) of author(s)
  ::
!:                                    ::  insert stack trace for this core
|%                                    ::  core with data-structures
++  move  (mold note gift)            ::  system calls
++  note  $:  %g  %mess  p=hapt       ::  requests we make
          q=ship  r=cage
          ==
++  gift                              ::  responses we give
  $:  %g
  $%  [%nice ~]                       ::  positive acknowledgment
      [%rush %json json]              ::  updates to subscribers
  ==  ==
++  hapt  ,[p=ship q=path]            ::  app instance
--
::
!:                                    ::  insert stack trace for this core
|_  $:  hid=hide                      ::  system data
      sent=(map ,@da message)         ::  app state data structures
    received=(map ,@da message)
    ==
++  prep  ,_`.                        ::  wipe state when app code is changed
::
++  peer                              ::  get 1st time subs. add them to sup
  |=  [ost=bone subs=ship pax=path]   ::  path and ship of subscriber
  ^-  [(list move) _+>.$]             ::  cast result to list moves, context
  =+  ^=  ordered-messages            ::  push on ordered list of all messages
  %+  sort                            ::  map with quicksort..
    (~(tap by received))              ::  ..over listified map
  |=  $:  a=(pair ,@da message)       ::  comparing by time
      b=(pair ,@da message)
      ==
  (gth p.a p.b)
  :_  +>.$                            ::  return unaltered app state
  :-  :^  ost  %give  %rush           ::  and send updated inbox
      :-  %json
      (inbox-to-json ordered-messages)
  ~
::
++  inbox-to-json                     ::  convert received messages to ++json
  |=  messes=(list ,[@da message])    ::  accept map of time to message
  :-  %a  ^-  (list json)             ::  compose json array of msgs
  %+  turn  messes                    ::  map over listified map of @da to msg
  |=  [tym=time mez=message]          ::  func that makes json msgs
  %^  jobe                            ::  compose ++json object
    tym/(jode tym)                    ::  k-v pair of tym:time
    mez/(msg-to-json mez)             ::  k-v pair of mez:++json msg
  ~
::
++  msg-to-json                       ::  convert msgs to json
  |=  mez=message                     ::  accept msg
  %^  jobe  to/s/(scot %p to.mez)     ::  compose ++json object of to:shipname
     subj/s/subj.mez                  ::  sub:subject
  :-  body/s/(role body.mez)          ::  body:body-of-msg
  ~                                   ::  jobe takes a list, needs ~ terminator
++  poke-message                      ::  receive data with mark of "message"
  |=  [ost=bone you=ship mez=message]
  ^-  [(list move) _+>.$]
  ?.  =(to.mez our.hid)               ::  unless we are receiving message:
    =.  sent                          ::  store sent message
    (~(put by sent) [lat.hid [you +.mez]])   
    ^-  [(list move) _+>.$]
    :_  +>.$                          ::  return new app stat
    :-  [ost %give %nice ~]
::  send message to other ship
  :_  ~
  :^  ost  %pass  /
  :^  %g  %mess  `hapt`[to.mez /urbmail]
  :-  `ship`our.hid  `cage`[%message !>(mez)]
  =.  received                        ::  if we're receiving: update received
  (~(put by received) [lat.hid [you +.mez]])
  =+  ^=  ordered-msgs                ::  push on sorted list of messages
  %+  sort                            ::  call sort on
    (~(tap by received))              ::  listified map..
  |=  [a=(pair ,@da message) b=(pair ,@da message)]
  (gth p.a p.b)                       ::  and gate that compares by @da
  :_  +>.$                            ::  return new app state
  :-  [ost %give %nice ~]             ::  positive acknowledgement
  %+  turn  (~(tap by sup.hid))       ::  map over list of subscribers
  |=  [os=bone *]                     
  :^  os  %give                       ::  sending them an update
    %rush
  :-(%json `json`(inbox-to-json ordered-msgs))          ::  of new inbox
 ::
++  pour                              ::  handle responses to requests we make
  |=  [ost=bone pax=path sih=*]
  ^-  [(list move) _+>.$]
  [[ost %give %nice ~]~ +>.$]         ::  confirm and return unaltered state
--
