::  blog generator
::
::  serves files from /=main=/pub/blog/src/writ
::  and allows basic editing
::
::  usage:
::  copy the files into your pier  
::  open http://localhost:8080/gen/main/pub/blog/fab/ to browse
::  open (for example)
::  http://localhost:8080/gin/your-ship/main/pub/blog/fab/~2014.9.3..18.02.28/ 
::  to edit
::
::::  /hook/hymn/blog/fab/pub/
  ::
/?    314
/=    gas  /$  fuel
/=    rut  /$  |=  [a=beam path]  
                 ^-  [p=path q=path]
               [(tope a(s ~)) (flop (slag 1 s.a))]
/=    post-manx
             /:  /=main=/pub/blog/src/writ
             /^  (pair (map ,@da ,[n=manx l=@da r=@da]) marl)
             /;  !:
                 |=  a=(list (pair ,@da manx))
                 :-  =<  res
                     %+  roll  a
                     |=  $:  [cur=@da man=manx]
                             res=(map ,@da ,[n=manx l=@da r=@da])
                             pre=@da
                         ==
                     :_  cur
                     =.  res
                       ?~  pre  res
                       =+  old=(~(got by res) pre)
                       (~(put by res) pre old(r cur))
                     (~(put by res) cur [man pre `@`~])
                 %+  turn
                   a
                 |=  [a=@da b=manx]
                   ;li
                     ;a(href "/gen/main/pub/blog/fab/{(scow %da a)}/")
                       ;-
                         %.  (snag 0 c.b)
                         |=  a=manx
                           ^-  tape
                           ?:  ?=(%$ n.g.a)
                             (poxo a)
                           (zing (turn c.a ..$))
                     ==
                   ==
             /&
             /psal/
/=    post-down
             /:  /=main=/pub/blog/src/writ
             /;  |=  [md=(list ,[@da @t])]
               (mo md)
             /&
             /md/
!:
::
::::  ~talsur-todres, ~fyr
  ::
=+  pax=`path`(flop but.gas)
=+  root="{<nyp.gas>}".
         "/{(trip q.bem.gas)}".
         "{<`path`(flop (slag 1 s.bem.gas))>}"
=+  tem=(~(get ju aut.ced.gas) %$)
=+  aut=(~(has in tem) (scot %p p.bem.gas))
::
^-  manx
;html
  ;head
    ;link(rel "stylesheet", href "/gen/main/pub/blog/src/main.css");
    ;*
      ?.  aut  ~
      ;=
        ;script(src "/gop/hart.js");
        ;script(src "/gon/main/lib/urb.js");
      ==
    ;+
      ?-  pax
      ~
        ;title: {<p.bem.gas>} %blog
      [@ ~]
        ;title: {(trip i.pax)} - {<p.bem.gas>} %blog
      *
        !!
      ==
  ==
  ;body
    ;div#c
      ;+    
        ?+  pax  !!
        ~
          ;div#list
            ;ul
              ;*  q.post-manx
            ==
          ==
        [@ ~]
          =+  poz=(~(got by p.post-manx) (slav %da i.pax))
          ;div#post
            ;a.btn(href root): Top
            ;br;
            ;*  ?~  l.poz  ~  :_  ~
                  ;a.btn(href "{root}/{<l.poz>}"): Prev
            ;*  ?~  r.poz  ~  :_  ~
                  ;a.btn(href "{root}/{<r.poz>}"): Next
            ;br;
            ;br;
            ;*  ?.  aut
              ~[n.poz]
            ;=
              ;script: spur = "pub/blog/src/writ/{(trip i.pax)}/md"
              ;script:'''
                      function sync(){
                        urb.send({
                          appl: "writ",
                          data: {
                            sup: window.spur,
                            bod: editor.value
                          }})
                      }
                      '''
              ;textarea#editor(onchange "sync()")
                ;-  (trip (~(got by post-down) (slav %da i.pax)))
          ==  ==          
  ==    ==  ==
==  ==
